// SPDX-License-Identifier: BUSL-1.1
pragma solidity 0.8.17;

import {Ownable} from "openzeppelin-contracts/contracts/access/Ownable.sol";
import {MutableUnlockersController} from "../controllers/MutableUnlockersController.sol";
import {PythSourceAdapter} from "../adapters/source-adapters/PythSourceAdapter.sol";
import {ChainlinkDestinationAdapter} from "../adapters/destination-adapters/ChainlinkDestinationAdapter.sol";
import {IAggregatorV3Source} from "../interfaces/chainlink/IAggregatorV3Source.sol";
import {BaseFactory} from "./BaseFactory.sol";
import {IPyth} from "../interfaces/pyth/IPyth.sol";

/**
 * @title OvalPyth is the recommended Oval contract that allows Oval to extract OEV generated by
 * Pyth price feeds and allow usage via the Chainlink interface.
 */
contract OvalPyth is MutableUnlockersController, PythSourceAdapter, ChainlinkDestinationAdapter {
    constructor(
        IPyth source,
        bytes32 pythPriceId,
        address[] memory unlockers,
        uint256 lockWindow,
        uint256 maxTraversal,
        uint256 maxAge,
        address owner
    )
        PythSourceAdapter(source, pythPriceId)
        MutableUnlockersController(lockWindow, maxTraversal, unlockers, maxAge)
        ChainlinkDestinationAdapter(18)
    {
        _transferOwnership(owner);
    }
}

/**
 * @title StandardPythFactory is the recommended factory for use cases that want a Pyth source and Chainlink
 * interface.
 * @dev This is the best factory for most use cases that need a Pyth source, but there are other variants that may be
 * needed if different mutability or interface choices are desired.
 */
contract StandardPythFactory is Ownable, BaseFactory {
    IPyth public immutable PYTH;

    constructor(IPyth _pyth, uint256 _maxTraversal, address[] memory _defaultUnlockers)
        BaseFactory(_maxTraversal, _defaultUnlockers)
    {
        PYTH = _pyth;
    }

    /**
     * @notice Creates the Pyth Oval instance.
     * @param pythPriceId the Pyth price id.
     * @param lockWindow the lockWindow used for this Oval instance. This is the length of the window
     * for the Oval auction to be run and, thus, the maximum time that prices will be delayed.
     * @param maxAge max age of a price that is used in place of the current price. If the only available price is
     * older than this, OEV is not captured and the current price is provided.
     * @return oval deployed oval address.
     */
    function create(bytes32 pythPriceId, uint256 lockWindow, uint256 maxAge) external returns (address oval) {
        oval = address(new OvalPyth(PYTH, pythPriceId, defaultUnlockers, lockWindow, MAX_TRAVERSAL, maxAge, owner()));
        emit OvalDeployed(msg.sender, oval, lockWindow, MAX_TRAVERSAL, owner(), defaultUnlockers);
    }
}
